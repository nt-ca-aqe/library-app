buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("se.transmode.gradle:gradle-docker:${dockerPluginVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

apply plugin: 'docker'
apply plugin: 'kotlin'
//apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = "1.2"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = "1.2"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}",
            "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
    compile("org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.boot:spring-boot-starter-aop",
            "org.springframework.boot:spring-boot-starter-amqp",
            "org.springframework.boot:spring-boot-starter-security",
            "org.springframework.boot:spring-boot-starter-web")
    compile("io.github.openfeign:feign-core:${feignVersion}",
            "io.github.openfeign:feign-jackson:${feignVersion}",
            "io.github.openfeign:feign-slf4j:${feignVersion}")
    compile("com.fasterxml.jackson.module:jackson-module-kotlin")
    compile("io.github.microutils:kotlin-logging:${kotlinLoggingVersion}")
    compile("io.micrometer:micrometer-registry-prometheus")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}",
            "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}")
    testCompile("io.rest-assured:rest-assured")
    testCompile("com.tngtech.archunit:archunit:${archunitVersion}")
    testCompile("com.github.tomakehurst:wiremock-standalone:${wiremockVersion}")
    testCompile("com.nhaarman.mockitokotlin2:mockito-kotlin:${mockitoKotlinVersion}")
    testCompile("info.novatec.testit:testutils-logrecorder-logback:${logRecorderVersion}")
    testCompile("au.com.dius:pact-jvm-consumer_2.12:${pactVersion}",
            "au.com.dius:pact-jvm-consumer-junit_2.12:${pactVersion}")
    testCompile("org.testcontainers:testcontainers:1.8.1")

    testImplementation("org.springframework.cloud:spring-cloud-starter-contract-stub-runner")

    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}",
            "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")

    // enable @ConfigurationProperties annotation processing
    // kapt("org.springframework.boot:spring-boot-configuration-processor")
    // annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
}

springBoot {
    buildInfo()
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}")
    }
}

task unitTest(type: Test) {
    useJUnitPlatform() { includeTags 'unit-test' }
    testLogging { events "skipped", "failed" }
}

task integrationTest(type: Test) {
    dependsOn unitTest
    useJUnitPlatform() { includeTags 'integration-test' }
    testLogging { events "skipped", "failed" }
}

task acceptanceTest(type: Test) {
    dependsOn integrationTest
    useJUnitPlatform() { includeTags 'acceptance-test' }
    testLogging { events "skipped", "failed" }
}

task aggregateTestReports(type: TestReport) {
    destinationDir = test.reports.html.destination
    reportOn unitTest, integrationTest, acceptanceTest
}

task buildDocker(type: Docker, dependsOn: build) {
    push = false
    tag = 'caaqe/library-enrichment'

    baseImage = 'openjdk:8-jre-alpine'
    volume("/tmp")
    addFile {
        from jar
        rename { 'app.jar' }
    }
    entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-jar', '/app.jar'])
}

tasks.check.dependsOn.remove(test)
tasks.check.dependsOn acceptanceTest
